// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    Not(in=zx, out=nzx);
    Not(in=zy, out=nzy);
    Spread16(in=nzx, out=nzxarr);
    And16(a=nzxarr, b=x, out=xz);
    Spread16(in=nx, out=nxarr);
    Xor16(a=xz, b=nxarr, out=xzn);

    Spread16(in=nzy, out=nzyarr);
    And16(a=nzyarr, b=y, out=yz);
    Spread16(in=ny, out=nyarr);
    Xor16(a=yz, b=nyarr, out=yzn);

    Spread16(in=f, out=farr);
    Not(in=f, out=nf);
    Spread16(in=nf, out=nfarr);
    Add16(a=xzn, b=yzn, out=xyadd);
    And16(a=xyadd, b=farr, out=xyaddand);

    And16(a=xzn, b=yzn, out=xyand);
    And16(a=xyand, b=nfarr, out=xyandand);

    Or16(a=xyaddand, b=xyandand, out=outf);

    Spread16(in=no, out=nos);
    Xor16(a=nos, b=outf, out=outno);
    TestZero(in=outno, out=zr);
    TestNega(in=outno, out=ng);
    Spread16(in=true, out=allone);
    And16(a=allone, b=outno, out=out);
}